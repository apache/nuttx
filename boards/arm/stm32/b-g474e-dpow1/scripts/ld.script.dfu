/****************************************************************************
 *  boards/arm/stm32/b-g474e-dpow1/scripts/ld.script
 *
 *  Licensed to the Apache Software Foundation (ASF) under one or more
 *  contributor license agreements.  See the NOTICE file distributed with
 *  this work for additional information regarding copyright ownership.  The
 *  ASF licenses this file to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance with the
 *  License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 *  WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 *  License for the specific language governing permissions and limitations
 *  under the License.
 *
 ****************************************************************************/

/* The STM32G474RE has 512 KiB of FLASH beginning at address 0x0800:0000.
 *
 * When booting from FLASH, FLASH memory is aliased to address 0x0000:0000
 * where the code expects to begin execution by jumping to the entry point in
 * the 0x0800:0000 address range. The FLASH bootloader is located there and
 * allocated up to 24KiB (6 pages of 4k if single bank mode or 12 pages of 2k
 * if dual bank mode), so our executable will begin at 0x0800:6000, leaving
 * 488KiB.
 *
 * The STM32G474RE has a total of 128 KiB of SRAM in three separate areas:
 *
 * 1) 80 KiB SRAM1 mapped at 0x2000:0000 thru 0x2001:3fff.
 * 2) 16 KiB SRAM2 mapped at 0x2001:4000 thru 0x2001:7fff.
 *
 * CCM SRAM (Routine Booster):
 *
 * 3) 32 KiB CCM SRAM mapped at 0x1000:0000 thru 0x1000:7fff
 *    but also aliased at at 0x2001:8000 thru 0x2001:ffff to be contiguous
 *    with the SRAM1 and SRAM2.
 *
 * Because SRAM1 and SRAM2 are contiguous, they are treated as one region
 * by this logic.
 *
 * CCM SRAM is also contiguous to SRAM1 and SRAM2, however it is excluded
 * from this linker script, to keep it reserved for special uses in code.
 * REVISIT: Is this the correct way to handle CCM SRAM?
 */

MEMORY
{
    flash (rx) : ORIGIN = 0x08006000, LENGTH = 488K
    sram (rwx) : ORIGIN = 0x20000000, LENGTH = 96K
}

OUTPUT_ARCH(arm)
EXTERN(_vectors)
ENTRY(_stext)

SECTIONS
{
    .text : {
        _stext = ABSOLUTE(.);
        *(.vectors)
        *(.text .text.*)
        *(.fixup)
        *(.gnu.warning)
        *(.rodata .rodata.*)
        *(.gnu.linkonce.t.*)
        *(.glue_7)
        *(.glue_7t)
        *(.got)
        *(.gcc_except_table)
        *(.gnu.linkonce.r.*)
        _etext = ABSOLUTE(.);
    } > flash

    .init_section : ALIGN(4) {
        _sinit = ABSOLUTE(.);
        *(.init_array .init_array.*)
        _einit = ABSOLUTE(.);
    } > flash

    .ARM.extab : ALIGN(4) {
        *(.ARM.extab*)
    } > flash

    .ARM.exidx : ALIGN(4) {
    __exidx_start = ABSOLUTE(.);
        *(.ARM.exidx*)
        __exidx_end = ABSOLUTE(.);
    } > flash

    _eronly = ABSOLUTE(.);

    .data : ALIGN(4) {
        _sdata = ABSOLUTE(.);
        *(.data .data.*)
        *(.gnu.linkonce.d.*)
        CONSTRUCTORS
        . = ALIGN(4);
        _edata = ABSOLUTE(.);
    } > sram AT > flash

    .bss : ALIGN(4) {
        _sbss = ABSOLUTE(.);
        *(.bss .bss.*)
        *(.gnu.linkonce.b.*)
        *(COMMON)
        . = ALIGN(4);
        _ebss = ABSOLUTE(.);
    } > sram

    /* Stabs debugging sections. */

    .stab 0 : { *(.stab) }
    .stabstr 0 : { *(.stabstr) }
    .stab.excl 0 : { *(.stab.excl) }
    .stab.exclstr 0 : { *(.stab.exclstr) }
    .stab.index 0 : { *(.stab.index) }
    .stab.indexstr 0 : { *(.stab.indexstr) }
    .comment 0 : { *(.comment) }
    .debug_abbrev 0 : { *(.debug_abbrev) }
    .debug_info 0 : { *(.debug_info) }
    .debug_line 0 : { *(.debug_line) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_aranges 0 : { *(.debug_aranges) }
}
