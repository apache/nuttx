/****************************************************************************
 * boards/risc-v/mpfs/icicle/scripts/ld-ihc-ch2.script
 *
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.  The
 * ASF licenses this file to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance with the
 * License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 ****************************************************************************/

MEMORY
{
    progmem (rx) : ORIGIN = 0xa3000000, LENGTH = 1M    /* w/ cache */
    filler  (rx) : ORIGIN = 0xa3100000, LENGTH = 524k
    sram   (rwx) : ORIGIN = 0xa3200000, LENGTH = 1M    /* w/ cache */
    shared (rwx) : ORIGIN = 0xa3000000, LENGTH = 256k
}

OUTPUT_ARCH("riscv")

__ksram_start = ORIGIN(sram);
__ksram_size = LENGTH(sram);
__ksram_end = ORIGIN(sram) + LENGTH(sram);

ENTRY(_stext)
EXTERN(__start)
SECTIONS
{
    .filler_area : ALIGN(0x1000)
    {
        __filler_area_load = LOADADDR(.filler_area);
        __filler_area_start = .;
        __filler_area_vma_start = .;
        *(.filler_area)
        KEEP(*(.filler_area))
        . = ALIGN(0x1000);
        __filler_area_end = .;
        __filler_area_vma_end = .;
    } > filler

    .amp_ihc : ALIGN(0x1000)
    {
        __amp_ihc_load = LOADADDR(.amp_ihc);
        __amp_ihc_start = .;
        __amp_ihc_vma_start = .;
        *(.amp_ihc)
        . = ALIGN(0x1000);
        __amp_ihc_end = .;
        __amp_ihc_vma_end = .;
    } > shared

    .text : {
        _stext = ABSOLUTE(.);
        *(.start .start.*)
        *(.text .text.*)
        *(.fixup)
        *(.gnu.warning)
        *(.rodata .rodata.* .srodata .srodata.*)
        *(.gnu.linkonce.t.*)
        *(.glue_7)
        *(.glue_7t)
        *(.got)
        *(.gcc_except_table)
        *(.gnu.linkonce.r.*)
        _etext = ABSOLUTE(.);
    } > progmem

    .init_section : ALIGN(4) {
        _sinit = ABSOLUTE(.);
        KEEP(*(.init_array .init_array.*))
        _einit = ABSOLUTE(.);
    } > progmem

    _eronly = ABSOLUTE(.);

    .data : ALIGN(4) {
        _sdata = ABSOLUTE(.);
        *(.data .data.*)
        *(.sdata .sdata.* .sdata2.*)
        *(.gnu.linkonce.d.*)
        *(.gnu.linkonce.s.*)
        CONSTRUCTORS
        . = ALIGN(4);
        _edata = ABSOLUTE(.);
    } > sram AT > progmem

    PROVIDE(__global_pointer$ = _sdata + ((_edata - _sdata) / 2));

    .bss : ALIGN(4) {
        _sbss = ABSOLUTE(.);
        *(.bss .bss.*)
        *(.sbss .sbss.*)
        *(.gnu.linkonce.b.*)
        *(.gnu.linkonce.sb.*)
        *(COMMON)
        . = ALIGN(32);
        _ebss = ABSOLUTE(.);
    } > sram

    /* Stabs debugging sections. */

    .stab 0 : { *(.stab) }
    .stabstr 0 : { *(.stabstr) }
    .stab.excl 0 : { *(.stab.excl) }
    .stab.exclstr 0 : { *(.stab.exclstr) }
    .stab.index 0 : { *(.stab.index) }
    .stab.indexstr 0 : { *(.stab.indexstr) }
    .comment 0 : { *(.comment) }
    .debug_abbrev 0 : { *(.debug_abbrev) }
    .debug_info 0 : { *(.debug_info) }
    .debug_line 0 : { *(.debug_line) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_aranges 0 : { *(.debug_aranges) }
}
